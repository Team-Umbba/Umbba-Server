bootJar { enabled = false }
jar { enabled = true }

dependencies {
    implementation project(path: ':umbba-common')

    // JPA & Database
    api "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation 'mysql:mysql-connector-java:8.0.32'
//    runtimeOnly "mysql:mysql-connector-java"

    // JSON
    implementation 'com.googlecode.json-simple:json-simple:1.1.1'

    // redis
    implementation "org.springframework.boot:spring-boot-starter-data-redis"
    implementation "org.springframework.session:spring-session-data-redis"

    // jackson
    implementation group: "io.jsonwebtoken", name: "jjwt-jackson", version: "0.11.2"

    // querydsl
    implementation "com.querydsl:querydsl-jpa"
    implementation "com.querydsl:querydsl-core"
    implementation "com.querydsl:querydsl-collections"
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa" // querydsl JPAAnnotationProcessor 사용 지정
    annotationProcessor "jakarta.annotation:jakarta.annotation-api" // java.lang.NoClassDefFoundError (javax.annotation.Generated) 대응 코드
    annotationProcessor "jakarta.persistence:jakarta.persistence-api" // java.lang.NoClassDefFoundError (javax.annotation.Entity) 대응 코드

}

def queryDslDir = "build/querydsl/generated"

sourceSets {
    main.java.srcDirs += [queryDslDir]
}

tasks.withType(JavaCompile) {
    options.getGeneratedSourceOutputDirectory().set(file(queryDslDir))
}

clean.doLast {
    file(queryDslDir).deleteDir()
}
